plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.suyanwar.samplecompose'
    compileSdk 33

    defaultConfig {
        applicationId "com.suyanwar.samplecompose"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            keyAlias "sample-compose-debug"
            keyPassword "sample_compose_debug"
            storeFile file('../sample_compose_debug.keystore')
            storePassword "sample_compose_debug"
        }
        release {
            keyAlias "sample-compose"
            keyPassword "sample_compose"
            storeFile file('../sample_compose.keystore')
            storePassword "sample_compose"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            crunchPngs false
            ext.alwaysUpdateBuildId = false
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        dev {
            initWith buildTypes.debug
            debuggable true
            buildConfigField "String", "URL_BASE", "\"https://dev.google.com\""
        }

        staging {
            initWith buildTypes.release
            debuggable true
            buildConfigField "String", "URL_BASE", "\"https://staging.google.com\""
        }

        live {
            initWith buildTypes.release
            debuggable false
            buildConfigField "String", "URL_BASE", "\"https://google.com\""
        }
    }

    flavorDimensions += "environment"

    variantFilter { variant ->
        if (variant.buildType.name.endsWith('release') || variant.buildType.name.endsWith('debug')) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }

    productFlavors {
        growth {
            dimension = "environment"
            resConfigs "xhdpi"
            versionNameSuffix "-growth"
            applicationId "com.suyanwar.samplecompose.growth"
        }
        experience {
            dimension = "environment"
            resConfigs "xhdpi"
            versionNameSuffix "-experience"
            applicationId "com.suyanwar.samplecompose.experience"
        }
        releaseCandidate {
            dimension = "environment"
            resConfigs "xhdpi"
            versionNameSuffix "-rc"
            applicationId "com.suyanwar.samplecompose.release"
        }
        sample {
            dimension = "environment"
            applicationId "com.suyanwar.samplecompose"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    devImplementation 'androidx.compose.ui:ui-tooling'
    devImplementation 'androidx.compose.ui:ui-test-manifest'
    stagingImplementation 'androidx.compose.ui:ui-tooling'
    stagingImplementation 'androidx.compose.ui:ui-test-manifest'
}